plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'kotlin'

    version = "0.3.4"
    group = "io.github.pellse"

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("17"))
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
        testImplementation 'org.hamcrest:hamcrest-library:1.3'
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
            testLogging {
                events("passed", "skipped", "failed")
            }
        }

        // show standard out and standard error of the test JVM(s) on the console
        // testLogging.showStandardStreams = true
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        repositories {
            maven {
                name = "ossrh"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty('ossrhUsername')
                    password = findProperty('ossrhPassword')
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = project.name
                    artifactId = project.name
                    packaging = 'jar'
                    description = 'Small library allowing to efficiently assemble entities from querying/merging external datasources or aggregating microservices'
                    url = 'https://github.com/pellse/assembler'

                    scm {
                        connection = 'scm:git@github.com:pellse/assembler.git'
                        developerConnection = 'scm:git@github.com:pellse/assembler.git'
                        url = 'https://github.com/pellse/assembler'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'pellse'
                            name = 'Sebastien Pelletier'
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
